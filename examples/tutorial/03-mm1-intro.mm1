delimiter $ ( ~ $  $ ) $;

provable sort wff;

term imp: wff > wff > wff;
infixr imp: $->$ prec 25;

term not: wff > wff;
prefix not: $~$ prec 100;

axiom ax_1 (a b: wff): $ a -> b -> a $;

axiom ax_2 (a b c: wff): $ (a -> b -> c) -> (a -> b) -> (a -> c) $;

axiom ax_3 (a b: wff): $ (~a -> ~b) -> (b -> a) $;

axiom ax_mp (a b: wff): $ a -> b $ > $ a $ > $ b $;

pub theorem id (a: wff): $ a -> a $ = '(ax_mp (ax_mp ax_2 ax_1) (! ax_1 _ $~a$));

local def and (a b) = $ ~(a -> ~b) $;
infixl and: $/\$ prec 35;

local def or (a b) = $ ~a -> b $;
infixl or: $\/$ prec 30;

theorem or_right: $ b -> a \/ b $ = '{ax_1 : $ _ -> _ -> _ $};

theorem a1i (h: $ b $): $ a -> b $ = '(ax_mp ax_1 h);
theorem a2i (h: $ a -> b -> c $): $ (a -> b) -> (a -> c) $ = '(ax_mp ax_2 h);
theorem mpd (h1: $ a -> b $) (h2: $ a -> b -> c $): $ a -> c $ = '(ax_mp (ax_mp ax_2 h2) h1);
theorem syl (h1: $ b -> c $) (h2: $ a -> b $): $ a -> c $ = '(mpd h2 (a1i h1));
theorem a3d (h: $ a -> ~b -> ~c $): $ a -> c -> b $ = '(syl ax_3 h);
theorem absurd: $ ~a -> a -> b $ = '(a3d ax_1);
theorem com12 (h: $ a -> b -> c $): $ b -> a -> c $ = '(syl (a2i h) ax_1);
theorem absurdr: $ a -> ~a -> b $ = '(com12 absurd);

theorem or_left: $ a -> a \/ b $ = 'absurdr;

-- To compile the file to make a proof:
-- * from examples/tutorial/
--   run: mm0-rs compile 03-mm1-intro.mm1 03-mm1-intro.mmb
-- * from mm0-c/
--   run: gcc main.c -o mm0-c
-- * go back to examples/tutorial/
--   run: ../../mm0-c/mm0-c 03-mm1-intro.mm1 < 03-mm1-intro.mmb
